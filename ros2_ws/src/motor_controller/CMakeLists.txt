cmake_minimum_required(VERSION 3.5)
project(motor_controller)

# Ensure executables can find packaged shared libraries at runtime
set(CMAKE_BUILD_RPATH "$ORIGIN/../lib")
set(CMAKE_INSTALL_RPATH "$ORIGIN/../lib")
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)


# include project wide configuration
include(${PROJECT_SOURCE_DIR}/../../cmake/project_config.cmake OPTIONAL)

# Set C++ standard to C++14
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Find necessary ROS 2 packages
find_package(ament_cmake REQUIRED)
find_package(rclcpp REQUIRED)
find_package(tf2_geometry_msgs REQUIRED)
find_package(volksface REQUIRED)
 

# uncomment the following line if msg/srv/action files are placed within this package
# finds the target generated by rosidl, necessary to link executables/libraries to target
# rosidl_get_typesupport_target(cpp_typesupport_target ${PROJECT_NAME} "rosidl_typesupport_cpp")

#------------------------------------------------------------------------------
# EPOS - library
#------------------------------------------------------------------------------
# Automatically detect architecture and link correct libEposCmd version
set(LIB_EPOS_FILE "libEposCmd.so.6.8.1.0")
set(LIB_FTDI_FILE "libftd2xx.so.1.4.8")

include(cmake/TargetArch.cmake)
target_architecture(TARGET_ARCH)
message("Detected architecture: ${TARGET_ARCH}")

if(${TARGET_ARCH} STREQUAL "x86_64")
  set(LIB_BASE_PATH "${PROJECT_SOURCE_DIR}/libEposCmd/intel/x86_64")
elseif(${TARGET_ARCH} STREQUAL "i386")
  set(LIB_BASE_PATH "${PROJECT_SOURCE_DIR}/libEposCmd/intel/x86")
elseif(${TARGET_ARCH} STREQUAL "armv6")
  set(LIB_BASE_PATH "${PROJECT_SOURCE_DIR}/libEposCmd/arm/v6")
elseif(${TARGET_ARCH} STREQUAL "armv7")
  set(LIB_BASE_PATH "${PROJECT_SOURCE_DIR}/libEposCmd/arm/v7")
elseif(${TARGET_ARCH} STREQUAL "armv8")
  set(LIB_BASE_PATH "${PROJECT_SOURCE_DIR}/libEposCmd/arm/v8")
else()
  message("Unsupported architecture for libEposCmd!")
  set(LIB_BASE_PATH "libEposCmd" PARENT_SCOPE)
endif()

set(EPOS_CMD_LIBRARY "${LIB_BASE_PATH}/${LIB_EPOS_FILE}")
set(FTDI_LIBRARY "${LIB_BASE_PATH}/${LIB_FTDI_FILE}")

message("Using EposCmd library: ${EPOS_CMD_LIBRARY}")
message("Using ftdi library:    ${FTDI_LIBRARY}")

# Install EPOS and FTDI shared libraries
install(FILES
  ${EPOS_CMD_LIBRARY}
  ${FTDI_LIBRARY}
  DESTINATION lib
)

# Create SONAME-compatible symlinks for EPOS
install(CODE "
  execute_process(COMMAND ln -sf ${LIB_EPOS_FILE} libEposCmd.so
                  WORKING_DIRECTORY \"\$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/lib\")
")

# Create SONAME-compatible symlinks for FTDI
install(CODE "
  execute_process(COMMAND ln -sf ${LIB_FTDI_FILE} libftd2xx.so
                  WORKING_DIRECTORY \"\$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/lib\")
")


add_library(EposCmd SHARED IMPORTED)
set_target_properties(EposCmd PROPERTIES
  IMPORTED_LOCATION ${EPOS_CMD_LIBRARY}
)

add_library(Ftdi SHARED IMPORTED)
set_target_properties(Ftdi PROPERTIES
  IMPORTED_LOCATION ${FTDI_LIBRARY}
)

# Define EPOS2 library
add_library(EPOS src/epos/epos.cpp)
target_link_libraries(EPOS EposCmd Ftdi "${cpp_typesupport_target}")
ament_target_dependencies(EPOS rclcpp volksface)

#------------------------------------------------------------------------------
# VMC - library
#------------------------------------------------------------------------------
# Define VMCLIB library
add_library(VMCLIB
  src/Adapter/CComAdapter.cpp
  src/LayerClasses/CVmc.cpp
  src/SupportClasses/CData.cpp
  src/SupportClasses/CRequest.cpp
  src/LayerClasses/CCommunicationLayer.cpp
  src/StorageClasses/CMotor.cpp
  src/SupportClasses/CError.cpp
  src/SupportClasses/CSendTwo.cpp
  src/LayerClasses/CTranslationLayer.cpp
  src/StorageClasses/CStorage.cpp
  src/SupportClasses/CMessage.cpp
  src/SupportClasses/CTimer.cpp
  src/LayerClasses/CvmcAPI.cpp
  src/SupportClasses/CChannel.cpp
  src/SupportClasses/CMultisend.cpp
  src/SupportClasses/CTimestamp.cpp
)
target_link_libraries(VMCLIB "${cpp_typesupport_target}")
ament_target_dependencies(VMCLIB rclcpp volksface)

#------------------------------------------------------------------------------
# Volksbot - Executable
#------------------------------------------------------------------------------
# Define volksbot_node executable
add_executable(motor_controller 
  src/controller.cc
)
set_target_properties(motor_controller PROPERTIES LINK_FLAGS "-pthread")
ament_target_dependencies(motor_controller
  rclcpp
  geometry_msgs
  volksface
)
target_link_libraries(motor_controller VMCLIB EPOS)

# Install libraries
install(
  TARGETS EPOS VMCLIB
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)

# export VMCLIB to use in other packages
install(TARGETS VMCLIB DESTINATION lib EXPORT mc-targets)
install(EXPORT mc-targets DESTINATION lib/${PROJECT_NAME})

# Install executables
install(
  TARGETS motor_controller
  DESTINATION lib/${PROJECT_NAME}
)

# Install launch files.
install(DIRECTORY
  launch
  DESTINATION share/${PROJECT_NAME}/
)

# Export dependencies and libraries
# if other packages includes the 'motor_controller' package, it automatically gets access 
# to the exported dependencies
ament_export_dependencies(rclcpp tf2_geometry_msgs volksface)

ament_package()